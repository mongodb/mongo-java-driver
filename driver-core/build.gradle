/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


plugins {
    id 'com.github.gmazzo.buildconfig' version '3.0.3'
}

archivesBaseName = 'mongodb-driver-core'
description = '''The Java operations layer for the MongoDB Java Driver.
 Third parties can wrap this layer to provide custom higher-level APIs'''

ext {
    pomName = 'MongoDB Java Driver Core'
}

// Add native-image.properties
sourceSets.main.resources.srcDirs = ['src/resources']

configurations {
    consumableTestRuntimeOnly {
        extendsFrom testRuntimeOnly
        canBeConsumed = true
    }
}

dependencies {
    api project(path: ':bson', configuration: 'default')

    implementation "com.github.jnr:jnr-unixsocket:$jnrUnixsocketVersion", optional
    api "io.netty:netty-buffer:$nettyVersion", optional
    api "io.netty:netty-transport:$nettyVersion", optional
    api "io.netty:netty-handler:$nettyVersion", optional
    implementation "org.xerial.snappy:snappy-java:$snappyVersion", optional
    implementation "com.github.luben:zstd-jni:$zstdVersion", optional
    implementation "org.mongodb:mongodb-crypt:$mongoCryptVersion", optional

    testImplementation project(':bson').sourceSets.test.output
    testRuntimeOnly "io.netty:netty-tcnative-boringssl-static:2.0.39.Final"
}

buildConfig {
    className('MongoDriverVersion')
    packageName('com.mongodb.internal.build')
    useJavaOutput()
    buildConfigField('String', 'NAME', '"mongo-java-driver"')
    buildConfigField('String', 'VERSION', "\"${gitVersion}\"")
}

afterEvaluate {
    jar.manifest.attributes['Automatic-Module-Name'] = 'org.mongodb.driver.core'
    jar.manifest.attributes['Bundle-SymbolicName'] = 'org.mongodb.driver-core'
    jar.manifest.attributes['Import-Package'] = [
            '!sun.misc.*',  // Used by DirectBufferDeallocator only for java 8
            '!sun.nio.ch.*',  // Used by DirectBufferDeallocator only for java 8
            'io.netty.*;resolution:=optional',
            'org.xerial.snappy.*;resolution:=optional',
            'com.github.luben.zstd.*;resolution:=optional',
            'org.slf4j.*;resolution:=optional',
            'jnr.unixsocket.*;resolution:=optional',
            'com.mongodb.crypt.capi.*;resolution:=optional',
            '*' // import all that is not excluded or modified before
    ].join(',')
}
