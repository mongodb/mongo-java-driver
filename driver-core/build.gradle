/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


plugins {
    alias(libs.plugins.build.config)
}

archivesBaseName = 'mongodb-driver-core'
description = 'Shared components for the Synchronous and Reactive Streams implementations of the MongoDB Java Driver.'

ext {
    pomName = 'MongoDB Java Driver Core'
}

configurations {
    consumableTestRuntimeOnly {
        extendsFrom testRuntimeOnly
        canBeConsumed = true
    }
}

dependencies {
    api project(path: ':bson', configuration: 'default')
    implementation project(path: ':bson-record-codec', configuration: 'default')
    implementation project(path: ':bson-kotlin', configuration: 'default'), optional
    implementation project(path: ':bson-kotlinx', configuration: 'default'), optional
    implementation project(path: ':mongodb-crypt', configuration: 'default'), optional

    implementation(libs.jnr.unixsocket, optional)
    api(platform(libs.netty.bom), optional)
    api(libs.bundles.netty, optional)
    compileOnly(libs.graal.sdk)

    // Optionally depend on both AWS SDK v2 and v1.
    // The driver will use v2 is present, v1 if present, or built-in functionality if neither are present
    implementation(libs.bundles.aws.java.sdk.v1, optional)
    implementation(libs.bundles.aws.java.sdk.v2, optional)

    implementation(libs.snappy.java, optional)
    implementation(libs.zstd.jni, optional)

    testImplementation project(':bson').sourceSets.test.output
    testImplementation(libs.bundles.junit)
    testImplementation(libs.reflections)

    testRuntimeOnly(libs.netty.boringssl)
    ["linux-x86_64", "linux-aarch_64", "osx-x86_64", "osx-aarch_64", "windows-x86_64"].each { arch ->
        testRuntimeOnly(variantOf(libs.netty.boringssl) { classifier(arch) })
    }
}

buildConfig {
    className('MongoDriverVersion')
    packageName('com.mongodb.internal.build')
    useJavaOutput()
    buildConfigField('String', 'NAME', '"mongo-java-driver"')
    buildConfigField('String', 'VERSION', "\"${gitVersion}\"")
}

afterEvaluate {
    jar.manifest.attributes['Automatic-Module-Name'] = 'org.mongodb.driver.core'
    jar.manifest.attributes['Bundle-SymbolicName'] = 'org.mongodb.driver-core'
    jar.manifest.attributes['Import-Package'] = [
            '!sun.misc.*',  // Used by DirectBufferDeallocator only for java 8
            '!sun.nio.ch.*',  // Used by DirectBufferDeallocator only for java 8
            '!javax.annotation.*', // Brought in by com.google.code.findbugs:annotations
            '!com.oracle.svm.core.annotate.*', // this dependency is provided by the GraalVM runtime
            'io.netty.*;resolution:=optional',
            'com.amazonaws.*;resolution:=optional',
            'software.amazon.awssdk.*;resolution:=optional',
            'org.xerial.snappy.*;resolution:=optional',
            'com.github.luben.zstd.*;resolution:=optional',
            'org.slf4j.*;resolution:=optional',
            'jnr.unixsocket.*;resolution:=optional',
            'com.mongodb.internal.crypt.capi.*;resolution:=optional',
            'jdk.net.*;resolution:=optional', // Used by SocketStreamHelper & depends on JDK version
            'org.bson.codecs.record.*;resolution:=optional', // Depends on JDK version
            'org.bson.codecs.kotlin.*;resolution:=optional',
            'org.bson.codecs.kotlinx.*;resolution:=optional',
            '*' // import all that is not excluded or modified before
    ].join(',')
}
