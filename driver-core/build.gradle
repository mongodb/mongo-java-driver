/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


plugins {
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

archivesBaseName = 'mongodb-driver-core'
description = '''The Java operations layer for the MongoDB Java Driver.
 Third parties can wrap this layer to provide custom higher-level APIs'''

ext {
    pomName = 'MongoDB Java Driver Core'
}

// Add native-image.properties
sourceSets.main.resources.srcDirs = ['src/resources']

dependencies {
    compile project(path: ':bson', configuration: 'default')

    compile "com.github.jnr:jnr-unixsocket:$jnrUnixsocketVersion", optional
    compile "io.netty:netty-buffer:$nettyVersion", optional
    compile "io.netty:netty-transport:$nettyVersion", optional
    compile "io.netty:netty-handler:$nettyVersion", optional
    compile "org.xerial.snappy:snappy-java:$snappyVersion", optional
    compile "com.github.luben:zstd-jni:$zstdVersion", optional
    compile "org.mongodb:mongodb-crypt:$mongoCryptVersion", optional

    testCompile project(':bson').sourceSets.test.output
}

buildConfig {
    appName = 'mongo-java-driver'
    version = project.gitVersion

    clsName = 'MongoDriverVersion'
    packageName = 'com.mongodb.internal.build'
}

afterEvaluate {
    jar.manifest.attributes['Automatic-Module-Name'] = 'org.mongodb.driver.core'
    jar.manifest.attributes['Bundle-SymbolicName'] = 'org.mongodb.driver-core'
    jar.manifest.attributes['Import-Package'] = [
        'org.bson.*', // unfortunate that this is necessary, but if it's left out then it's not included
        'javax.crypto.*',
        'javax.crypto.spec.*',
        'javax.management.*',
        'javax.naming.*',
        'javax.naming.directory.*',
        'javax.net.*',
        'javax.net.ssl.*',
        'javax.security.sasl.*',
        'javax.security.auth.callback.*',
        'org.ietf.jgss.*',
        'io.netty.*;resolution:=optional',
        'org.xerial.snappy.*;resolution:=optional',
        'com.github.luben.zstd.*;resolution:=optional',
        'org.slf4j;resolution:=optional',
        'jnr.unixsocket;resolution:=optional'
    ].join(',')
}
