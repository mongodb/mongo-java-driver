/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * Configures publishing of the main java projects
 */

// Publishing helpers
ext {
    configurePom = { project ->
        { ->
                name = project.hasProperty('pomName') ? project.getProperty('pomName') : project.name
                description = project.description
                url = project.hasProperty('pomURL') ? project.getProperty('pomURL') : 'https://www.mongodb.com/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    url = 'https://github.com/mongodb/mongo-java-driver'
                    connection = 'scm:https://github.com/mongodb/mongo-java-driver.git'
                    developerConnection = 'scm:git@github.com:mongodb/mongo-java-driver.git'
                }
                developers {
                    developer {
                        name = 'Various'
                        organization = 'MongoDB'
                    }
                }
            }
    }
    configureMavenRepositories = { project ->
        { ->
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            maven {
                url = project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username project.hasProperty('nexusUsername') ? project.getProperty('nexusUsername') : ''
                    password project.hasProperty('nexusPassword') ? project.getProperty('nexusPassword') : ''
                }
            }

            maven {
                url = "$rootDir/build/repo"
            }
        }
    }
    configureJarManifestAttributes = { project ->
        { ->
            manifest.attributes['-exportcontents'] =  "*;-noimport:=true"
            manifest.attributes['Build-Version'] =  project.gitVersion
            manifest.attributes['Bundle-Version'] =  project.version
            manifest.attributes['Bundle-Name'] =  project.archivesBaseName
            manifest.attributes['Bundle-SymbolicName'] =  project.group + '.' + project.archivesBaseName
        }
    }
}


def projectNamesNotToBePublished = ["driver-benchmarks", "driver-lambda", "driver-workload-executor", "graalvm-native-image-app", "util",
                                    "spock", "taglets"]
def publishedProjects = subprojects.findAll { !projectNamesNotToBePublished.contains(it.name) }
def bomProjects = project(":bom")
def scalaProjects = publishedProjects.findAll { it.name.contains('scala') } - bomProjects
def javaProjects = publishedProjects - scalaProjects - bomProjects
def projectsWithManifest = publishedProjects.findAll {it.name != 'driver-legacy' } - bomProjects

configure(javaProjects) { project ->
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    task sourcesJar(type: Jar) {
        from project.sourceSets.main.allJava
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.archivesBaseName
                from project.components.java
                artifact sourcesJar
                artifact javadocJar

                suppressPomMetadataWarningsFor("dateTimeSupportApiElements")
                suppressPomMetadataWarningsFor("dateTimeSupportRuntimeElements")

                suppressPomMetadataWarningsFor("jsonSupportApiElements")
                suppressPomMetadataWarningsFor("jsonSupportRuntimeElements")

                suppressPomMetadataWarningsFor("mongoCryptSupportApiElements")
                suppressPomMetadataWarningsFor("mongoCryptSupportRuntimeElements")
            }
        }

        repositories configureMavenRepositories(project)
    }

    afterEvaluate {
        publishing.publications.mavenJava.artifactId = project.archivesBaseName
        publishing.publications.mavenJava.pom configurePom(project)
        signing {
            useInMemoryPgpKeys(findProperty("signingKey"), findProperty("signingPassword"))
            sign publishing.publications.mavenJava
        }
    }
}

configure(scalaProjects) { project ->
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    task sourcesJar(type: Jar) {
        from project.sourceSets.main.allScala
        classifier = 'sources'
    }

    task scaladocJar(type: Jar) {
        from scaladoc
        classifier = 'javadoc'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.archivesBaseName.contains('bson') ? 'mongo-scala-bson' : 'mongo-scala-driver'
                from project.components.java
                artifact sourcesJar
                artifact scaladocJar
            }
        }

        repositories configureMavenRepositories(project)
    }

    afterEvaluate {
        publishing.publications.mavenJava.pom configurePom(project)
        signing {
            useInMemoryPgpKeys(findProperty("signingKey"), findProperty("signingPassword"))
            sign publishing.publications.mavenJava
        }
    }
}


configure(projectsWithManifest) { project ->
    apply plugin: 'biz.aQute.bnd.builder'
    afterEvaluate {
        jar configureJarManifestAttributes(project)
    }
}

configure(bomProjects) { project ->
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'java-platform'

    // Get the Scala versions from the project property. Only major.minor versions.
    def scalaVersions = project.findProperty("scalaVersions")?.split(",")
            ?.collect { it.split("\\.")[0] + "." + it.split("\\.")[1] }

    assert scalaVersions != null && !scalaVersions.isEmpty() : "Scala versions must be provided as a comma-separated list" +
            " in the 'scalaVersions' project property"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = "bom".equals(project.archivesBaseName) ? "mongodb-driver-bom" : project.archivesBaseName
                from components.javaPlatform

                // Modify the generated POM to add multiple compile versions of driver-scala or bson-scala.
                // Scala multi-version support generates only one for BOM.
                pom.withXml {
                    def pomXml = asNode()

                    def dependencyManagementNode = pomXml.get("dependencyManagement")?.getAt(0)
                    assert dependencyManagementNode : "<dependencyManagement> node not found in the generated BOM POM"

                    def dependenciesNode = dependencyManagementNode.get("dependencies")?.getAt(0)
                    assert dependenciesNode : "<dependencies> node not found inside <dependencyManagement>"

                    // Check if scala dependencies are present in the BOM.
                    def existingScalaDeps = dependenciesNode.children().findAll {
                        it.artifactId.text().contains("scala")
                    }

                    existingScalaDeps.each { existingDep ->
                        String groupId = existingDep.groupId.text()
                        String originalArtifactId = existingDep.artifactId.text()
                        String artifactVersion = existingDep.version.text()

                        // Add multiple versions with Scala suffixes for each Scala-related dependency.
                        scalaVersions.each { scalaVersion ->
                            // Remove existing Scala version suffix (_2.12, _2.13, etc.)
                            String baseArtifactId = originalArtifactId.replaceAll("_\\d+\\.\\d+(\\.\\d+)?\$", "")
                            String newArtifactId = "${baseArtifactId}_${scalaVersion}"

                            // Skip if Scala dependency with this scalaVersion already exists in BOM.
                            if(newArtifactId != originalArtifactId) {
                                def dependencyNode = dependenciesNode.appendNode("dependency")
                                dependencyNode.appendNode("groupId", groupId)
                                dependencyNode.appendNode("artifactId", newArtifactId)
                                dependencyNode.appendNode("version", artifactVersion)
                            }
                        }
                    }
                }
            }
        }

        repositories configureMavenRepositories(project)
    }

    afterEvaluate {
        publishing.publications.mavenJava.pom configurePom(project)
        signing {
            useInMemoryPgpKeys(findProperty("signingKey"), findProperty("signingPassword"))
            sign publishing.publications.mavenJava
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    tasks.withType(GenerateMavenPom).configureEach {
        doLast {
            def xml = file(destination).text
            def root = new groovy.xml.XmlSlurper().parseText(xml)

            def dependencies = root.dependencyManagement.dependencies.children()
            assert dependencies.children().size() > 1 : "BOM must contain more then one <dependency> element:\n$destination"

            dependencies.each { dependency ->
                def groupId = dependency.groupId.text()
                assert groupId.startsWith('org.mongodb') : "BOM must contain only 'org.mongodb' dependencies, but found '$groupId':\n$destination"
                /* The <scope> and <optional> tags should be omitted in BOM dependencies.
                   This ensures that consuming projects have the flexibility to decide whether a
                   dependency is optional in their context. The BOM's role is to provide version information,
                   not to dictate inclusion or exclusion of dependencies. */
                assert dependency.scope.size() == 0 : "BOM must not contain <scope> elements in dependency:\n$destination"
                assert dependency.optional.size() == 0 : "BOM must not contain <optional> elements in dependency:\n$destination"
            }
        }
    }
}
