/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = "A Scala wrapper of the MongoDB Reactive Streams Java driver"
archivesBaseName = 'mongo-scala-driver'

dependencies {
    compile project(path: ':bson-scala', configuration: 'default')
    compile project(path: ':driver-reactive-streams', configuration: 'default')

    testImplementation project(':util')
    testImplementation project(':driver-sync')
    testImplementation project(':bson').sourceSets.test.output
    testImplementation project(':driver-sync').sourceSets.test.output
    testImplementation project(':driver-core').sourceSets.test.output
    testImplementation project(':driver-reactive-streams').sourceSets.test.output
    testRuntimeOnly project(path: ':driver-core', configuration: 'consumableTestRuntimeOnly')
}


configurations {
    integrationTestCompile.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        scala.srcDir file('src/it/scala')
        scala {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

test {
    maxParallelForks = 1
}

tasks.withType(Test) {
    doFirst {
        println("Running Test task using scala version: $scalaVersion")
    }
}

task integrationTest(type: Test) {
    delete 'build/test-it'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn integrationTest

task aggregatedScalaDoc(type: ScalaDoc) {
    description('Unified Scaladoc for bson-scala and driver-scala')
    group('documentation')
    ext.fromProjects = [
            project(':bson-scala'),
            project(':driver-scala')
    ]
    destinationDir = file("${rootProject.buildDir.path}/docs/${project.archivesBaseName}")
    classpath = project.sourceSets.main.compileClasspath
    source = fromProjects.collect { it.sourceSets.main.allSource }

    scalaDocOptions.additionalParameters = ["-doc-root-content", "${project.rootDir}/driver-scala/rootdoc.txt" as String]

    // Workaround for gradle 2.13 scaladoc issue: https://github.com/gradle/gradle/issues/9855
    if (project.scalaVersion.startsWith("2.13")) {
        scalaClasspath += files("$rootDir/gradle/scala/lib/scala-ant-2.13.1.jar")
    }
}

idea {
    module {
        testSourceDirs += sourceSets.integrationTest.scala.srcDirs
        testResourceDirs += sourceSets.integrationTest.resources.srcDirs
        scopes.TEST.plus += [ configurations.integrationTestCompile ]
    }
}


ext {
    pomName = 'Mongo Scala Driver'
}

afterEvaluate {
    jar.manifest.attributes['Automatic-Module-Name'] = 'org.mongodb.scala.mongo-scala-driver'
    jar.manifest.attributes['Import-Package'] = [
            'org.bson.*',
            'com.mongodb.*',
    ].join(',')
}
