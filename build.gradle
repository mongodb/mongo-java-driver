/*
 * Copyright (c) 2008 - 2013 10gen, Inc. <http://10gen.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def configDir = new File(rootDir, 'config')

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'me.trnl:clirr-gradle-plugin:0.4'
    }
}

//////////////////////////////////////////
// Common behavior                      //
//////////////////////////////////////////

configure(subprojects.findAll { it.name != 'util' }) {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'jacoco'

    evaluationDependsOn(':util')

    group = 'org.mongodb'
    version = '3.0.0-SNAPSHOT'
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile project(':util') //Adding categories to classpath
    }

    sourceSets {
        main { java.srcDirs = ['src/main'] }
        test { java.srcDirs = ['src/test'] }
    }

    /* Compiling */
    tasks.withType(AbstractCompile) {
        options.encoding = 'ISO-8859-1'
        options.fork = true
        options.debug = true
        options.compilerArgs = ['-Xlint:all', '-Xlint:-options']

        onlyIf { JavaVersion.current().isJava7Compatible() }
    }

    project.ext.buildingWith = { n ->
        project.hasProperty(n) && project.property(n).toBoolean()
    }

    /* Testing */
    tasks.withType(Test) {
        maxParallelForks = 1
        systemProperties(
                'org.mongodb.test.uri': System.getProperty('org.mongodb.test.uri', null),
                'org.mongodb.useSocket': System.getProperty('org.mongodb.useSocket', 'false'),
                'org.mongodb.disableAsync': System.getProperty('org.mongodb.disableAsync', 'false'),
                'javax.net.ssl.trustStore': System.getProperty('javax.net.ssl.trustStore', "${System.getProperty('user.home')}/.keystore"),
       	        'javax.net.ssl.keyStore': System.getProperty('javax.net.ssl.keyStore', "${System.getProperty('user.home')}/.keystore"),
                'javax.net.ssl.keyStorePassword': System.getProperty('javax.net.ssl.keyStorePassword', 'changeit'),
                'javax.net.ssl.trustStorePassword': System.getProperty('javax.net.ssl.trustStorePassword', 'changeit')
            
        )

        if (project.buildingWith('ssl.enabled')) {
            systemProperties(
                    'javax.net.ssl.keyStoreType': project.property('ssl.keyStoreType'),
                    'javax.net.ssl.keyStore': project.property('ssl.keyStore'),
                    'javax.net.ssl.keyStorePassword': project.property('ssl.keyStorePassword'),
                    'javax.net.ssl.trustStoreType': project.property('ssl.trustStoreType'),
                    'javax.net.ssl.trustStore': project.property('ssl.trustStore'),
                    'javax.net.ssl.trustStorePassword': project.property('ssl.trustStorePassword')
            )
        }

        useJUnit {
            if (!JavaVersion.current().isJava7Compatible() || project.buildingWith('ssl.enabled')) {
                excludeCategories 'category.Async'
            }
            if (!project.buildingWith('rs.enabled')) {
                excludeCategories 'category.ReplicaSet'
            }
        }

        jacoco { enabled = false }

        beforeTest { descr ->
            logger.info("[Test ${descr.className} > ${descr.name}]")
        }
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(check)) {
            tasks.withType(Test) { jacoco { enabled = true } }
        }
    }

    task testAll(dependsOn: tasks.withType(Test))
    check.dependsOn testAll

    /* Code quality */
    checkstyle {
        configFile = new File("$configDir/checkstyle.xml")
    }

    findbugs {
        excludeFilter = new File("$configDir/findbugs-exclude.xml")
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = project.buildingWith('xmlReports.enabled')
            html.enabled = !project.buildingWith('xmlReports.enabled')
        }
    }

    javadoc {
        dependsOn project(':util').compileJava //We need taglets to be compiled
        options.author = true
        options.version = true
        options.links 'http://docs.oracle.com/javase/7/docs/api/'
        options.tagletPath project(':util').sourceSets.main.output.classesDir
        options.taglets 'ManualTaglet'
    }

}

//////////////////////////////////////////
// Project specific behavior            //
//////////////////////////////////////////

project(':bson') {}

project(':driver') {
    apply plugin: 'groovy'
    apply plugin: 'codenarc'

    sourceSets {
        test {
            groovy { srcDirs = ['src/test/unit'] }
            java { srcDirs = ['src/test/unit'] }
        }
        functional {
            java { srcDirs = ['src/test/functional'] }
            groovy { srcDirs = ['src/test/functional'] }
        }
        //Note: no groovy/spock tests in acceptance, these are Java tests
        acceptance { java.srcDirs = ['src/test/acceptance'] }
    }

    dependencies {
        compile project(':bson')
        testCompile 'org.codehaus.groovy:groovy-all:2.0.8'
        testCompile ('org.spockframework:spock-core:0.7-groovy-2.0') {
            exclude group: 'junit', module: 'junit-dep'
        }
        testCompile 'cglib:cglib-nodep:2.2.2'
        testCompile 'org.objenesis:objenesis:1.3'
        functionalCompile sourceSets.test.runtimeClasspath
        acceptanceCompile sourceSets.functional.runtimeClasspath
    }

    task functionalTest(type: Test, dependsOn: test) {
        testClassesDir = sourceSets.functional.output.classesDir
        classpath = sourceSets.functional.runtimeClasspath
    }

    task acceptanceTest(type: Test, dependsOn: functionalTest) {
        testClassesDir = sourceSets.acceptance.output.classesDir
        classpath = sourceSets.acceptance.runtimeClasspath
    }


    codenarc {
        toolVersion = '0.18.1'
        reportFormat = project.buildingWith('xmlReports.enabled') ? 'xml' : 'html'
    }

    task uberJar(type: Jar) {
        classifier = 'all'
        from project(':bson').sourceSets.main.output
        from sourceSets.main.output
    }
}

project(':driver-compat') {
    apply plugin: 'clirr'
    apply plugin: 'groovy'
    apply plugin: 'codenarc'

    sourceSets {
        test {
            groovy.srcDirs = ['src/test/unit']
            java.srcDirs = ['src/test/unit']
        }
        legacy {
            java.srcDirs = ['src/test/legacy']
        }
    }

    dependencies {
        compile project(':driver')
        testCompile project(':driver').sourceSets.test.output
        testCompile 'org.codehaus.groovy:groovy-all:2.0.8'
        testCompile ('org.spockframework:spock-core:0.7-groovy-2.0') {
            exclude group: 'junit', module: 'junit-dep'
        }
        testCompile 'cglib:cglib-nodep:2.2.2'
        testCompile 'org.objenesis:objenesis:1.3'
        legacyCompile sourceSets.test.runtimeClasspath
    }


    task legacyTest(type: Test, dependsOn: test) {
        testClassesDir = sourceSets.legacy.output.classesDir
        classpath = sourceSets.legacy.runtimeClasspath
    }

    clirr {
        ignoreFailures = true
        excludeFilter = new File("$configDir/clirr-exclude.yml")
        baseline 'org.mongodb:mongo-java-driver:2.11.2'
    }

    checkstyle {
        configFile = new File("$configDir/checkstyle-lite.xml")
    }

    codenarc {
        toolVersion = '0.18.1'
        reportFormat = project.buildingWith('xmlReports.enabled') ? 'xml' : 'html'
    }

    task uberJar(type: Jar) {
        classifier = 'all'
        from project(':bson').sourceSets.main.output
        from project(':driver').sourceSets.main.output
        from sourceSets.main.output
    }
}

//////////////////////////////////////////
// Root project configuration           //
//////////////////////////////////////////
task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}

gradle.buildFinished { BuildResult result ->
    if (result.failure && !JavaVersion.current().isJava7Compatible()) {
        gradle.rootProject.logger.error("\n* Warning:\nJDK ${JavaVersion.VERSION_1_7} is minimal requirement for building the driver. You have ${JavaVersion.current()}.")
    }
}

apply from: 'gradle/ide-settings.gradle'
apply from: 'gradle/maven-deployment.gradle'
apply from: 'gradle/osgi-compatibility.gradle'
