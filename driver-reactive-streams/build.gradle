/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = "A Reactive Streams implementation of the MongoDB Java driver"
archivesBaseName = 'mongodb-driver-reactivestreams'

dependencies {
    api project(path: ':bson', configuration: 'default')
    api project(path: ':driver-core', configuration: 'default')

    api 'org.reactivestreams:reactive-streams:1.0.4'
    implementation platform("io.projectreactor:reactor-bom:$projectReactorVersion")
    implementation 'io.projectreactor:reactor-core'

    testImplementation project(':bson').sourceSets.test.output
    testImplementation project(':driver-sync')
    testImplementation project(':driver-sync').sourceSets.test.output
    testImplementation project(':driver-core').sourceSets.test.output
    testImplementation 'org.reactivestreams:reactive-streams-tck:1.0.4'
    testImplementation 'io.projectreactor:reactor-test'
    if ('8'.equals(findProperty("javaVersion"))) {
        testImplementation 'org.mockito:mockito-junit-jupiter:4.6.1'
    } else {
        testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    }

    testRuntimeOnly project(path: ':driver-core', configuration: 'consumableTestRuntimeOnly')
}

sourceSets {
    test.groovy.srcDirs += ['src/examples']
    test.java.srcDirs += ['src/test/tck']
}

test {
    exclude 'tour/**'
}

// Reactive Streams TCK uses TestNG, and Gradle can't run TestNG and JUnit in the same run.
task tckTest(type: Test) {
    useTestNG()
    scanForTestClasses = false

    classpath = sourceSets.test.runtimeClasspath
    testClassesDirs = sourceSets.test.output.classesDirs

    binaryResultsDirectory.set(file("$buildDir/$name-results/binary"))
    reports {
        html.destination = file("$buildDir/reports/$name")
        junitXml.destination = file("$buildDir/$name-results")
    }

    maxParallelForks = 1
}

ext {
    pomName = 'The MongoDB Reactive Streams Driver'
}

afterEvaluate {
    jar.manifest.attributes['Automatic-Module-Name'] = 'org.mongodb.driver.reactivestreams'
    jar.manifest.attributes['Bundle-SymbolicName'] = 'org.mongodb.driver-reactivestreams'
    jar.manifest.attributes['Import-Package'] = [
            'com.mongodb.crypt.capi.*;resolution:=optional',
            'com.mongodb.internal.crypt.capi.*;resolution:=optional',
            '*',
    ].join(',')
}
