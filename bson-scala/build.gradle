/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

description = "A Scala wrapper / extension to the bson library"
archivesBaseName = 'mongo-scala-bson'

dependencies {
    api project(path: ':bson', configuration: 'default')
}

sourceSets {
    main {
        scala {
            if(scalaVersion.startsWith("2.13")) {
                srcDirs = ["src/main/scala", "src/main/scala-2.13+"]
            } else {
                srcDirs = ["src/main/scala", "src/main/scala-2.13-"]
            }
        }
    }
}

tasks.withType(Test) {
    doFirst {
        println("Running Test task using scala version: $scalaVersion")
    }
}

// ===================
//     Scala checks
// ===================
tasks.register("scalaCheck") {
    description = "Runs all the Scala checks"
    group = "verification"

    dependsOn("clean", "compileTestScala", "check")
    tasks.findByName("check").mustRunAfter("clean")
}

ext {
    pomName = 'Mongo Scala Bson Library'
}

test {
    maxParallelForks = 1
}

afterEvaluate {
    jar.manifest.attributes['Automatic-Module-Name'] = 'org.mongodb.bson.scala'
    jar.manifest.attributes['Import-Package'] = [
            '!scala.*',
            '*'
    ].join(',')
}
