/*
 * Copyright (c) 2008-2014 MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'osgi'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.kordamp.gradle.clirr'

def configDir = new File(rootDir, 'config')
archivesBaseName = 'bson'

clirr {
    excludeFilter = new File("$configDir/clirr-exclude.yml")
    baseline 'org.mongodb:bson:3.2.0'
    failOnErrors = true
}

jar {
    manifest {
        instruction 'Build-Version', getGitVersion()
        instruction 'Import-Package',
                    'javax.xml.bind.*',
                    'org.slf4j;resolution:=optional'
    }
}

modifyPom {
    project {
        name 'BSON'
        description 'The BSON library'
        url 'http://bsonspec.org'
    }
}

dependencies {
    shadow 'com.fasterxml:classmate:1.3.1'
}

shadowJar {
    mergeServiceFiles()
    relocate 'com.fasterxml', 'org.bson.shaded.fasterxml'
    classifier = ''
    dependencies {
        exclude(dependency('org.slf4j:.*'))
    }
}

configurations {
    compile.extendsFrom shadow
}

artifacts {
    archives shadowJar
}
